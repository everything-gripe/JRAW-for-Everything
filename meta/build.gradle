import net.dean.jraw.gradle.CredentialsTask

repositories {
    // KPoet requirement
    maven { url "https://www.jitpack.io" }
}

dependencies {
    implementation 'org.jsoup:jsoup:1.10.2'
    implementation 'com.squareup:javapoet:1.9.0'
    implementation 'com.github.agrosner:KPoet:1.0.0'
    implementation 'javassist:javassist:3.12.1.GA'
    implementation 'org.reflections:reflections:0.9.11'
    implementation 'net.steppschuh.markdowngenerator:markdowngenerator:1.3.0.0'
    implementation project(':lib')

    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
}

task update(type: JavaExec, dependsOn: classes) {
    main = 'net.dean.jraw.meta.MainKt'
    classpath = sourceSets.main.runtimeClasspath

    // Extract source dir from the lib project's "gen" set for Java files
    def generatedJavaDir = findProject(':lib').sourceSets.getByName('gen').java.srcDirs[0]
    args = [
        "--enum", generatedJavaDir,
        "--md-overview", file("../ENDPOINTS.md").absolutePath,
        "--readme-badge", file("../README.md").absolutePath,
        "--version", generatedJavaDir.absolutePath + ";" + project.version
    ]
}

task credentials(type: CredentialsTask)

